<header>         := -[\n]*
vrmlScene        := rootItem*
>rootItem<       := ts,(Proto/ExternProto/ROUTE/('USE',ts,USE,ts)/Script/Node),ts
<Proto>          := 'PROTO',ts,nodegi,ts,'[',ts,(fieldDecl/eventDecl)*,']', ts, '{', ts, vrmlScene,ts, '}', ts
fieldDecl	 := fieldExposure,ts,dataType,ts,name,ts,Field,ts
fieldExposure    := 'field'/'exposedField'
dataType         := 'SFBool'/'SFString'/'SFFloat'/'SFTime'/'SFVec3f'/'SFVec2f'/'SFRotation'/'SFInt32'/'SFImage'/'SFColor'/'SFNode'/'MFBool'/'MFString'/'MFFloat'/'MFTime'/'MFVec3f'/'MFVec2f'/'MFRotation'/'MFInt32'/'MFColor'/'MFNode'
eventDecl        := eventDirection, ts, dataType, ts, name, ts
eventDirection   := 'eventIn'/'eventOut'
<ExternProto>    := 'EXTERNPROTO',ts,nodegi,ts,'[',ts,(extFieldDecl/eventDecl)*,']', ts, ExtProtoURL
extFieldDecl     := fieldExposure,ts,dataType,ts,name,ts
ExtProtoURL      := '['?,(ts,SFString)*, ts, ']'?, ts  # just an MFString by another name :)
<ROUTE>          := 'ROUTE',ts, name,'.',name, ts, 'TO', ts, name,'.',name, ts
Node             := (Def)?,nodegi,ts,'{',ts,(Proto/ExternProto/ROUTE/Attr)*,ts,'}', ts
<Script>         := (Def)?,'Script',ts,'{',ts,(ScriptFieldDecl/ScriptEventDecl/Proto/ExternProto/ROUTE/Attr)*,ts,'}', ts
ScriptEventDecl  := eventDirection, ts, dataType, ts, name, ts, ('IS', ts, IS,ts)?
ScriptFieldDecl  := fieldExposure,ts,dataType,ts,name,ts,(('IS', ts,IS,ts)/Field),ts
SFNull           := 'NULL', ts

# should really have an optimised way of declaring a different reporting name for the same production...
Def              :='DEF',ts,name,ts
USE              := name
IS               := name
>nodegi<         := name
Attr             := name, ts, (('IS', ts,IS,ts)/Field), ts
Field            := ( '[',ts,((SFNumber/SFBool/SFString/('USE',ts,USE,ts)/Script/Node),ts)*, ']', ts )/((SFNumber/SFBool/SFNull/SFString/('USE',ts,USE,ts)/Script/Node),ts)+

name             := -[][0-9{}\000-\020"'#,.\\ ],  -[][{}\000-\020"'#,.\\ ]*
SFNumber         := [-+]*, ( ('0',[xX],[0-9]+) / ([0-9.]+,([eE],[-+0-9.]+)?))
SFBool           := 'TRUE'/'FALSE'
SFString         := '"',(CHARNODBLQUOTE/ESCAPEDCHAR/SIMPLEBACKSLASH)*,'"'
<CHARNODBLQUOTE> :=  -[\134"]+
<SIMPLEBACKSLASH>:= '\134'
<ESCAPEDCHAR>    := '\\"'/'\134\134'
<ts>             :=  ( [ \011-\015,]+ / ('#',-'\012'*,'\n')+ )*
